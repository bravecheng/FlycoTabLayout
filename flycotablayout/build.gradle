apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 212
        versionName "2.1.2-androidx"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api 'androidx.legacy:legacy-support-v4:1.0.0'
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'com.flyco.tablayout'
            version = android.defaultConfig.versionName
            artifactId 'FlycoTabLayout_Lib'
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            //把API依赖也放入POM
            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each {
                    // 如果有compile fileTree()，group会为空，需要去除
                    if (it.group != null) {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = repo_url
    publish {
        repository {
            repoKey = repo_key
            username = repo_username
            password = repo_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            publishPom = true
        }
    }
}

